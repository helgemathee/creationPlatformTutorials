use GeometryAttributesRT;
use PolygonMeshRT;
use Vec3;

operator WaveDeformerPerVertex(
  Index i,
  io Vec3 positions[],
  Vec3 axis,
  Vec3 center,
  Scalar amplitude,
  Scalar frequency,
  Scalar speed,
  Scalar time
)
{
  Vec3 pos = positions[i];

  // compute some wave deformation
  Vec3 localPos = pos - center;
  Scalar distance = axis.dot(localPos);
  Vec3 onAxisPos = center + axis * distance;
  localPos = pos - onAxisPos;
  Scalar factor = sin(distance * frequency + time * speed) * amplitude;
  localPos *= 1.0 + factor;
  pos = onAxisPos + localPos;
    
  positions[i] = pos;
}

operator WaveDeformer(
  io PolygonMesh mesh,
  io Vec3 axis,
  io Vec3 center,
  io Scalar amplitude,
  io Scalar frequency,
  io Scalar speed,
  io Scalar time
) {
  // get the positions attribute and its values
  GeometryAttributes attributes = mesh.getAttributes();
  Vec3Attribute posAttr = attributes.getPositions();
  Vec3 positions[] = posAttr.values;

  // invoke multithreading
  executeParallel(
    WaveDeformerPerVertex,
    positions.size(),
    positions,
    axis,
    center,
    amplitude,
    frequency,
    speed,
    time
  );

  // notify of the change
  posAttr.incrementVersion();
  
  // recompute normals
  mesh.computeNormals();
}