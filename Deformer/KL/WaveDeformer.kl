

use PolygonMeshRT;
use GeometryAttributesRT;
use Vec3;

operator WaveDeformer(
  io PolygonMesh mesh,
  io GeometryAttributes attributes,
  io Vec3 axis,
  io Vec3 center,
  io Scalar amplitude,
  io Scalar frequency
) {

  // pull out positions data
  Scalar positions[] = attributes.scalarAttributes[Attribute_Pos].data;
  
  for(Size i=0; i < attributes.size(); i++) {
    
    Size index = i * 3;
    
    // pull out a single positions
    Vec3 pos(positions[index+0],positions[index+1],positions[index+2]);
    
    // compute some wave deformation
    Vec3 localPos = pos - center;
    Scalar distance = axis.dot(localPos);
    Vec3 onAxisPos = center + axis * distance;
    localPos = pos - onAxisPos;
    Scalar factor = sin(distance * frequency) * amplitude;
    localPos *= 1.0 + factor;
    pos = onAxisPos + localPos;
    
    // push back a single position
    positions[index+0] = pos.x;
    positions[index+1] = pos.y;
    positions[index+2] = pos.z;
  }
  
  // push back the positions data
  attributes.scalarAttributes[Attribute_Pos].data = positions;
  
  // recompute normals
  mesh.computeNormals(attributes);
}